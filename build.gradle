plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
}

group = 'de.hhn.se.labswp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.slf4j:slf4j-api:1.7.24'
	implementation 'org.slf4j:slf4j-simple:1.7.25'

	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

configurations.all {
	exclude module: 'logback-classic'
}

war {
	archiveName 'SHTravel.war'
	enabled=true
}

/* start integrating vue into jar file on build */
processResources {
	dependsOn 'copyFrontendToBuild'
}

task copyFrontendToBuild(type: Copy) {
	dependsOn 'npmBuild'
	from "$projectDir/src/frontend/dist/"
	into "$buildDir/resources/main/static"
}

copyFrontendToBuild.configure {
	onlyIf{(project.hasProperty('full'))}
}

task npmBuild(type: Exec) {
	workingDir './src/frontend/'
	commandLine 'npm', 'run', 'build'
}

npmBuild.configure {
	onlyIf{(project.hasProperty('full'))}
}
/* end of integrating vue into jar file on build */

test {
	useJUnitPlatform()
}
